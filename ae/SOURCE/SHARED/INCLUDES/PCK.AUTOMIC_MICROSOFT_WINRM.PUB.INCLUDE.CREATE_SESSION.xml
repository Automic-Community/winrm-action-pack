<?xml version="1.0" encoding="ISO-8859-15" standalone="no"?><uc-export clientvers="11.0.0">
<JOBI name="PCK.AUTOMIC_MICROSOFT_WINRM.PUB.INCLUDE.CREATE_SESSION">
<HEADER state="1">
<Title>Create Session</Title>
<ArchiveKey1/>
<ArchiveKey2/>
<OH_SubType/>
</HEADER>
<SCRIPT mode="1" state="1">
<MSCRI><![CDATA[


:SET &UC4RB_WRM_CRED_CMDLET# = "$credential = New-Object System.Management.Automation.PSCredential( "

!    Username
:    INC PCK.AUTOMIC_MICROSOFT_WINRM.PUB.INCLUDE.ESCAPE_ARG_VALUE "&TEMP_OPTION_VALUE#" = "&UC4RB_WRM_USERNAME#"
:    SET &UC4RB_WRM_CRED_CMDLET# = STR_CAT(&UC4RB_WRM_CRED_CMDLET#, '"&UC4RB_WRM_ESC_VALUE#", $secPwd)')


! --- Create a PSCredential Object containing the username and password ---
:    SET &UC4RB_WRM_SESSION_CMDLET# = "$session = New-PSSession"
:    SET &UC4RB_WRM_PSO_CMDLET# = "$sessionOption = New-PSSessionOption"

!    Buidling Connect command
!    Host name
:    INC PCK.AUTOMIC_MICROSOFT_WINRM.PUB.INCLUDE.ESCAPE_ARG_VALUE "&TEMP_OPTION_VALUE#" = "&UC4RB_WRM_HOST#"
:    SET &UC4RB_WRM_SESSION_CMDLET# = STR_CAT(&UC4RB_WRM_SESSION_CMDLET#, ' -ComputerName "&UC4RB_WRM_ESC_VALUE#"')

!    Port
:    IF &UC4RB_WRM_PORT# NE ""
:      SET &UC4RB_WRM_SESSION_CMDLET# = STR_CAT(&UC4RB_WRM_SESSION_CMDLET#, ' -Port "&UC4RB_WRM_PORT#"')
:    ENDIF

!    Credential
:    SET &UC4RB_WRM_SESSION_CMDLET# = STR_CAT(&UC4RB_WRM_SESSION_CMDLET#, " -Credential $credential")

!    Use SSL
:    IF &UC4RB_WRM_SSL# EQ "YES"
:        SET &UC4RB_WRM_SESSION_CMDLET# = STR_CAT(&UC4RB_WRM_SESSION_CMDLET#, " -UseSSL")
:    ENDIF

!    Authentication Type
:    IF &UC4RB_WRM_AUTH_TYPE# NE ""
:      SET &UC4RB_WRM_SESSION_CMDLET# = STR_CAT(&UC4RB_WRM_SESSION_CMDLET#, " -Authentication  &UC4RB_WRM_AUTH_TYPE# ")
:    ENDIF

!    Skip CA Check
:    IF &UC4RB_WRM_CA_CHECK# EQ "YES"
:      SET &UC4RB_WRM_PSO_CMDLET# = STR_CAT(&UC4RB_WRM_PSO_CMDLET#, " -SkipCACheck")
:    ENDIF

!    Skip CN Check
:    IF &UC4RB_WRM_CN_CHECK# EQ "YES"
:      SET &UC4RB_WRM_PSO_CMDLET# = STR_CAT(&UC4RB_WRM_PSO_CMDLET#, " -SkipCNCheck")
:    ENDIF

:    SET &CONN_TIMEOUT# = MULT(&UC4RB_WRM_CONN_TIMEOUT#, 1000)
!    Connection Timeout
:    SET &UC4RB_WRM_PSO_CMDLET# = STR_CAT(&UC4RB_WRM_PSO_CMDLET#, " -OpenTimeout &CONN_TIMEOUT#")

:    SET &UC4RB_WRM_SESSION_CMDLET# = STR_CAT(&UC4RB_WRM_SESSION_CMDLET#, " -SessionOption $sessionOption")

! Initialize the error
$errors = 0
! Load powershell utility
: INC PCK.ITPA_SHARED.PUB.INCLUDE.POWERSHELL_UTILITY
!   Set the parameter to stop in order to terminate the script execution on error
$ErrorActionPreference= 'Stop'
!   To ignore DisplayDeprecationWarnings
$WarningPreference= 'SilentlyContinue'
! --- Create a PSCredential Object containing the username and password ---
!    DECRYPT PASSWORD
:   INC PCK.ITPA_SHARED.PUB.INCLUDE.ATTACH
:   SET &UC4RB_ENCRYPTED_PASSWORD_TMP# = &UC4RB_WRM_PASSWORD#
:   INC PCK.ITPA_SHARED.PRV.INCLUDE.DECRYPT_PASSWORD
! Securing password
$secPwd = ConvertTo-SecureString "$UC4_DECRYPTED_PWD" -AsPlainText -Force
! Genearting credential using username and password
:   P  "Credential commandlet: &UC4RB_WRM_CRED_CMDLET#"

&UC4RB_WRM_CRED_CMDLET#

!    Session advance option commandlet
:    P  "Advance session option commandlet: &UC4RB_WRM_PSO_CMDLET#"
      &UC4RB_WRM_PSO_CMDLET#

!    Creation session commandlet
:    P "Creation session commandlet: &UC4RB_WRM_SESSION_CMDLET#"
     &UC4RB_WRM_SESSION_CMDLET#]]></MSCRI>
</SCRIPT>
<DOCU_Docu state="1" type="text">
<DOC><![CDATA[Create a session with the remote host using WinRM. ]]></DOC>
</DOCU_Docu>
</JOBI>
</uc-export>